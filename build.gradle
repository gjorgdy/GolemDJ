plugins {
    id "com.modrinth.minotaur" version "2.+"
    id 'fabric-loom' version "${loom_version}"
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}


repositories {
    maven {
        name = "FzzyMaven"
        url = "https://maven.fzzyhmstrs.me/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "me.fzzyhmstrs:fzzy_config:${project.fzzy_config_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}

static String getModrinthToken() {
    return new File('.env')
            .readLines()
            .findAll { it?.trim() && !it.trim().startsWith('#') }
            .collect { it.split('=') }
            .find { it[0] == 'MODRINTH_TOKEN' }[1]
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
modrinth {
    autoAddDependsOn = false
    token = getModrinthToken()
    projectId = project.mod_id
    versionName = "${project.archives_base_name} ${version}"
    versionNumber = "${version}"
    changelog = rootProject.file("changelog.md").text
    syncBodyFrom = rootProject.file("README.md").text
    versionType = project.mod_release_type
    uploadFile = remapJar
    gameVersions = project.minecraft_versions.split(',') as List<String>
    loaders = ["fabric"]
    dependencies {
        optional.version "fzzy-config", project.fzzy_config_version
    }
}